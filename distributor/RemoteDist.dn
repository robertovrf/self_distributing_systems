uses data.adt.List

data Int {
	int i
}

const char debugMSG[] = "[@RemoteDist]"
const int DEFAULT_DIST_PORT = 8081
const int DEFAULT_APP_PORT  = 2010

component provides App requires io.Output out, net.TCPSocket, net.TCPServerSocket,
	data.StringUtil strUtil, composition.Assembly assembly, data.json.JSONEncoder encoder,
	data.IntUtil iu, os.SystemInfo sys, io.FileSystem fileSystem, io.TextFile {

	IDCReference remote[] 	= null
	int portDist 			= 0
	int portApp				= 0
	bool running 			= false

	void startRemoteProcess(char remoteP[], char state[]) {
		// load and start relocated component
		fileSystem.delete("/tmp/ready")
		if (!running) {
			AppParam params[] = new AppParam[1]
			params[0] = new AppParam()
			params[0].string = iu.intToString(portApp)
			assembly.setMain(remoteP, params)
			assembly.removeComp(new String[](
				new String("./data/adt/ListCPPropagate.o"),
				new String("./data/adt/ListCPAlternate.o"),
				new String("./data/adt/ListCPSharding.o"),
				new String("./data/adt/ListCPDynamicSharding.o")
			))
			out.println("$(debugMSG) Remote Component $(remoteP) Started! ")
			running = true
			remote = assembly.getCompsByProvidedInterface("proxy.DistProxy")
		}
		if ((remote == null) || (remote.arrayLength > 1)) { throw new Exception("Something went wrong!") }

		// parsing state to Data
		Int content[] = new Int[]
		if (state != null && state != "") {
			content = encoder.jsonToArray(state, typeof(Int[]), null)
		}

		// adding content to list in the object
		Object objects[] = remote[0].class.getObjects("data.adt.List:heap")
		List list = objects[0]
		for (int i = 0; i < content.arrayLength; i++) {
			list.add(content[i])
		}
		out.println("$(debugMSG) State inserted!")

		TextFile f = new TextFile(new char[]("/tmp/ready"), File.WRITE)
		f.writeLine("ok")
		f.flush()
		f.close()
	}

	// TODO: need to pause component, wait for object to finish processing
	// any threads it is currently running, etc.
	void clearState() {
		// adding content to list in the object
		Object objects[] = remote[0].class.getObjects("data.adt.List:heap")
		List list = objects[0]
		while (list.getLength() > 0) {
			list.remove(list.getFirst())
		}
	}

	void handleRequest(TCPSocket client) {
		// reading data from socket -- this could be optimised
		char buf[] = null
		int len = 0
		char command[] = null
		while ((buf = client.recv(1)).arrayLength > 0) {
			command = new char[](command, buf)
			len++
			//stop condition
			if (len >= 4) {
				if ((command[len-4] == "\r") && (command[len-3] == "\r") &&
					(command[len-2] == "\r") && (command[len-1] == "\r")) {
					break
				}
			}
		}
		// processing command
		String helper[] = strUtil.explode(command, "!")
		if (helper[0].string == "../distributor/RemoteList.o") {
			startRemoteProcess(helper[0].string, helper[1].string)
		} else {
			clearState()
		}
		client.send("OK")
		client.disconnect()
	}

	int App:main(AppParam params[]) {
		if (params.arrayLength == 2) {
			portDist = iu.intFromString(params[0].string)
			portApp = iu.intFromString(params[1].string)
		} else {
			portDist = DEFAULT_DIST_PORT
			portApp = DEFAULT_APP_PORT
		}

		char impl[] = sys.getVariable("IMPLEMENTATION")
		if (impl != "") {
			char state[] = sys.getVariable("STATE")
			out.println("Starting remote dist with implementation $(impl)")
			startRemoteProcess(impl, state)
		}

		// instantiating variables
		TCPServerSocket host = new TCPServerSocket()

		// binding server socket
		if (!host.bind(TCPServerSocket.ANY_ADDRESS, portDist)) {
			out.println("Error: failed to bind master socket")
			return 1
		}

		out.println("$(debugMSG) Remote Distributor started!")
		while (true) {
			TCPSocket client = new TCPSocket()
			if (client.accept(host))
				asynch::handleRequest(client)
		}

		return 0
	}
}
