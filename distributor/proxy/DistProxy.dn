data Param {
	char value[]
}

data Request {
	char functionName[]
	int numParams
	Param params[]
}

data Response {
	// 1 OK - 2 FAILED
	byte status
	// if it's null or "" this has to be translated to null
	char value[]
}

data Int {
	int i
}

/* Available list operations */
const char ADD[]          = "add"
const char GET_LENGTH[]   = "getLength"
const char GET_CONTENTS[] = "getContents"

component provides proxy.DistProxy requires io.Output out, data.IntUtil iu, net.TCPSocket,
	net.TCPServerSocket, data.adt.List:heap list, data.json.JSONEncoder parser, data.StringUtil strUtil  {

	bool serviceStatus
	TCPServerSocket host

	Response addRequest(Request request) {
		Response response = new Response()

		Int item = parser.jsonToData(request.params[0].value, typeof(Int), null)
		list.add(item)

		response.status = 1
		response.value = ""

		return response
	}

	Response getLengthRequest(Request request) {
		Response response = new Response()

		response.status = 1
		response.value = new char[](iu.intToString(list.getLength()))

		return response
	}

	Response getContentsRequest(Request request) {
		Response response = new Response()

		Data nums[] = list.getContents()
		Int result[] = new Int[nums.arrayLength]
		for (int i = 0; i < nums.arrayLength; i++) {
			result[i] = nums[i]
		}
		response.status = 3
		response.value = parser.jsonFromArray(result, null)

		return response
	}

	Response interpretRequest(Request request) {
		if (request.functionName == ADD) {
			return addRequest(request)

		} else if (request.functionName == GET_LENGTH) {
			return getLengthRequest(request)

		} else if (request.functionName == GET_CONTENTS) {
			return getContentsRequest(request)

		} else {
			Response response = new Response()
			response.status = 2
			response.value = ""
			return response
		}
	}

	Request parseRequest(char content[]) {
		String helper[] = strUtil.explode(content, "!")
		Request request = parser.jsonToData(helper[0].string, typeof(Request), null)
		Request newRequest = new Request()
		newRequest.numParams = request.numParams
		newRequest.functionName = request.functionName
		for (int i = 0; i < newRequest.numParams; i++) {
			newRequest.params = new Param[newRequest.numParams]()
			newRequest.params[i] = new Param()
			newRequest.params[i].value = helper[i+1].string
		}
		return newRequest
	}

	Request readRequest(TCPSocket s) {
		Request request = null
		char buf[] = null
		int len = 0
		char command[] = null
		while ((buf = s.recv(1)).arrayLength > 0) {
			command = new char[](command, buf)
			len++
			//stop condition
			if (len >= 4) {
				if ((command[len-4] == "\r") && (command[len-3] == "\r") &&
					(command[len-2] == "\r") && (command[len-1] == "\r")) {
					request = parseRequest(strUtil.subString(command,
							0, command.arrayLength-4))
					break
				}
			}
		}
		if (request == null) { s.disconnect() }
		return request
	}

	void processRequest(TCPSocket s) {
		Response response = new Response()
		Request request = readRequest(s)
		char content[]
		if (request == null) {
			response.status = 2
			response.value = null
		} else {
			response = interpretRequest(request)
		}
		if (response.status == 3) {
			response.status = 2
			char value[] = response.value
			response.value = ""
			content = new char[](parser.jsonFromData(response,null), "!", value, "\r\r\r\r")
		} else {
			content = new char[](parser.jsonFromData(response,null), "\r\r\r\r")
		}
		s.send(content)
		s.disconnect()
	}

	void DistProxy:start(int port) {
		out.println("STARTED")
		serviceStatus = true
		host = new TCPServerSocket()
		if (!host.bind(TCPServerSocket.ANY_ADDRESS, port)) {
			out.println("Error: failed to bind master socket")
			return
		}
		while (serviceStatus) {
			TCPSocket client = new TCPSocket()
			if (client.accept(host)) {
				asynch::processRequest(client)
			}
		}
	}
}
